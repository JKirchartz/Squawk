; 119 cycles
samplecruncher:
    push r18                       ; 2
    push r17                       ; 2
    push r16                       ; 2
    push r0                        ; 2
    push r1                        ; 2
    push r2                        ; 2
    in   r2, __SREG__              ; 1

    lds  r18, ch+2*c_size+c_freq   ; 2    oscillator 3 - setup
    lds  r0, ch+2*c_size+c_phase   ; 2    essentially
    add  r0, r18                   ; 1    phase += frequency
    sts  ch+2*c_size+c_phase, r0   ; 2
    lds  r18, ch+2*c_size+c_freq+1 ; 2
    lds  r1, ch+2*c_size+c_phase+1 ; 2
    adc  r1, r18                   ; 1
    sts  ch+2*c_size+c_phase+1, r1 ; 2

    mov  r17, r1                   ; 1
    sbrc r17, 7                    ; \_2  oscillator 3 - triangle
    com  r17                       ; /
    lsl  r17                       ; 1
    lds  r16, ch+2*c_size*c_vol    ; 2
    subi r17, 128                  ; 1
    muls r17, r16                  ; 2
    lsl  r1                        ; 1
    mov  r16, r1                   ; 1
        
    lds  r18, ch+0*c_size+c_freq   ; 2    oscillator 1 - setup
    lds  r0, ch+0*c_size+c_phase   ; 2    essentially
    add  r0, r18                   ; 1    phase += frequency
    sts  ch+0*c_size+c_phase, r0   ; 2
    lds  r18, ch+0*c_size+c_freq+1 ; 2
    lds  r1, ch+0*c_size+c_phase+1 ; 2
    adc  r1, r18                   ; 1
    sts  ch+0*c_size+c_phase+1, r1 ; 2

    mov  r18, r1                   ; 1    oscillator 1 - 25% pulse
    lsl  r18                       ; 1
    and  r18, r1                   ; 1
    lds  r17, ch+0*c_size+c_vol    ; 2
    sbrc r18, 7                    ; \_2
    neg  r17                       ; /
    add  r16, r17                  ; 1

    lds  r18, ch+1*c_size+c_freq   ; 2    oscillator 2 - setup
    lds  r0, ch+1*c_size+c_phase   ; 2    essentially
    add  r0, r18                   ; 1    phase += frequency
    sts  ch+1*c_size+c_phase, r0   ; 2
    lds  r18, ch+1*c_size+c_freq+1 ; 2
    lds  r1, ch+1*c_size+c_phase+1 ; 2
    adc  r1, r18                   ; 1
    sts  ch+1*c_size+c_phase+1, r1 ; 2

    lds  r17, ch+1*c_size+c_vol    ; 2    oscillator 2 - square wave
    sbrc r1, 7                     ; \_2
    neg  r17                       ; /
    add  r16, r17                  ; 1

    ldi  r17, 1                    ; 1    oscillator 4 - setup
    lds  r0, ch+3*c_size+c_freq    ; 2    essentially
    lds  r1, ch+3*c_size+c_freq+1  ; 2    linear feedback shift register
    add  r0, r0                    ; 1
    adc  r1, r1                    ; 1
    sbrc r1, 7                     ; \_2
    eor  r0, r17                   ; /
    sbrc r1, 6                     ; \_2
    eor  r0, r17                   ; /
    sts  ch+3*c_size+c_freq, r0    ; 2
    sts  ch+3*c_size+c_freq+1, r1  ; 2

    lds  r17, ch+3*c_size+c_vol    ; 2    oscillator 4 - 1bit noise
    sbrc r1, 7                     ; \_2
    neg  r17                       ; /
    add  r16, r17                  ; 1

    subi r16, 128                  ; 1    convert to unsigned
    sts  OCR3AL, r16               ; 1    pwm output @ timer3, oc-a
                                   
    out  __SREG, r2                ; 2
    pop  r2                        ; 2
    pop  r1                        ; 2
    pop  r0                        ; 2
    pop  r16                       ; 2
    pop  r17                       ; 2
    pop  r18                       ; 2
                                   
    reti                           ; 4